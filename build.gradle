
import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java-library'
    id "com.github.spotbugs" version "4.7.1" apply false
    id "net.kyori.blossom" version "1.2.0" apply false
    id 'com.github.johnrengelman.shadow' version '7.0.0' apply false
}

allprojects {
    group 'com.vexsoftware'
    version '3.0.0-SNAPSHOT'
}

subprojects {
    apply plugin: 'java'
    apply plugin: "com.github.spotbugs"

    repositories {
        mavenCentral()
        maven {
            url "https://hub.spigotmc.org/nexus/content/repositories/snapshots"
        }
        maven {
            url "https://oss.sonatype.org/content/repositories/snapshots"
        }
        maven {
            url "https://repo.spongepowered.org/maven"
        }
        maven {
            url "https://repo.velocitypowered.com/snapshots/"
        }
    }

    dependencies {
        testImplementation "org.junit.jupiter:junit-jupiter-api:5.7.2"
        testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.7.2"
        testImplementation "org.junit.jupiter:junit-jupiter-params:5.7.2"
        testImplementation "org.mockito:mockito-core:3.10.0"
    }

    processResources {
        filter(ReplaceTokens, tokens: ["app.version": this.project.version])
    }

    test {
        useJUnitPlatform()
    }

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(11)
            vendor = JvmVendorSpec.ADOPTOPENJDK
        }
    }

    tasks.withType(com.github.spotbugs.snom.SpotBugsTask) {
        reports {
            html {
                enabled = true
                destination = file("$buildDir/reports/spotbugs/main/spotbugs.html")
                stylesheet = 'fancy-hist.xsl'
            }
        }
    }
}
